######## Takes a matrix of matrices (a pattern of blocks), and assembles it into a bigmat

# i index
# n length
# p ptr (using the colptr compression convention - the index of start of a swath within a vector)
#
# b block  - indexing blocks within pattern
# l local  - indexing entries inside a block
# g global - indexing the whole bigmat
#
# r row
# c col
# v non-zero value
#
# sparse.rowval ‚Üí ilr
# sparse.colptr ‚Üí pilr  
# ilv    = pilr[ilc]+ i-1
# irow   = ilr[ilv]  

using Printf

"""
    bigmat,bigmatasm,bigvecasm,bigvecdis = prepare(pattern)

Prepare for the assembly of sparse blocks into a large sparse matrix. 
`bigmat` is allocated, with the correct sparsity structure, but its `nzval` undef'ed.    
Where some blocks share the same sparsity structure, `blocks` in `pattern` can have `===` elements.

`pattern` is a `SparseMatrixCSC{<:SparseMatrixCSC}`, where empty blocks are structuraly zero

See also: [`addin!`](@ref)
""" 
function prepare(pattern::SparseMatrixCSC{SparseMatrixCSC{Tv,ùï´},ùï´}) where{Tv} 
    nbr,nbc                       = size(pattern)
    nbr>0 && nbc>0 || muscadeerror("must have size(pattern,i)>0 ‚àÄi‚àà[1,2]")
    nlr                           = [-1 for ibr=1:nbr+1] # number of local rows in each block-row of pattern
    nlc                           = [-1 for ibc=1:nbc+1] # number of local cols in each block-col of pattern
    ngv                           = 0
    asm_igv                       = Vector{ùï´1}(undef,nnz(pattern))
    for ibc                       = 1:nbc
        for ibv                   = pattern.colptr[ibc]:pattern.colptr[ibc+1]-1
            ibr                   = pattern.rowval[ibv]
            block                 = pattern.nzval[ ibv]
            nlr[ibr+1]==-1 || nlr[ibr+1]==block.m || muscadeerror(@printf "block row %i of the pattern has blocks with different numbers of rows")
            nlc[ibc+1]==-1 || nlc[ibc+1]==block.n || muscadeerror(@printf "block column %i of the pattern has blocks with different numbers of columns")
            nlr[ibr+1]            = block.m
            nlc[ibc+1]            = block.n
            nlv                   = length(block.nzval)
            ngv                  += nlv
            asm_igv[ibv]          = ùï´1(undef,nlv) # allocate: for each block in pattern, where in bigmat value to put block value 
        end
    end
    nlr[1]                        = 1
    nlc[1]                        = 1
    if any(nlr.==-1) || any(nlc.==-1) 
        for i = findall(nlr.==-1)
            @printf("    row    %i of the pattern has only empty blocks\n",i)
        end
        for i = findall(nlc.==-1)
            @printf("    column %i of the pattern has only empty blocks\n",i)
        end
        muscadeerror("invalid sparse-of-sparse pattern")
    end
    pgr                           = cumsum(nlr)  # pgr[ibr]‚Üíigr global row corresponding to the first local row of each block
    pgc                           = cumsum(nlc)  # pgc[ibc]‚Üíigc global column corresponding to the first local row of each block
    ngr                           = pgr[end]-1
    ngc                           = pgc[end]-1

    # create asm and global matrix (gv, aka nzval is undef in global matrix)
    pigr                          = ùï´1(undef,ngc+1)        # aka global.colptr
    igr                           = ùï´1(undef,ngv  )        # aka global.rowval
    gv                            = Vector{Tv}(undef,ngv)  # aka global.nzval
    asm                           = Vector{ùï´2}(undef,nbr)  # asm[ibc][ibr,ilc] ‚Üí igv for a given block, and local column, where does the storage start?
    pigr[1]                       = 1
    igv                           = 1
    for ibc                       = 1:nbc                  # for each block column
        for ilc                   = 1:nlc[ibc+1]           # for each local column
            igc                   = pgc[ibc]-1 + ilc 
            for ibv               = pattern.colptr[ibc]:pattern.colptr[ibc+1]-1 # for each block row
                ibr               = pattern.rowval[ibv]
                block             = pattern.nzval[ ibv]
                pilr,ilr          = block.colptr, block.rowval
                for ilv           = pilr[ilc]:pilr[ilc+1]-1 # for each value in block row
                    igr[igv]      = pgr[ibr]-1 + ilr[ilv]   # row of corresponding global value
                    asm_igv[ibv][ilv] = igv                 # index of global value for block value and local value 
                    igv          += 1    
                end
            end
            pigr[igc+1]           = igv   
        end
    end
    bigmat    = SparseMatrixCSC(ngr,ngc,pigr,          igr,           gv     )
    bigmatasm = SparseMatrixCSC(nbr,nbc,pattern.colptr,pattern.rowval,asm_igv) # The assembler is a sparse, with same sparsity as pattern, which values are vectors telling where the values of a block go in the values of bigmat
    return    bigmat,bigmatasm,pgr,pgc    
end
"""
    addin!(asm,global,block,ibr,ibc,factor=1.)

Add a sparse `block` into a large `out` sparse matrix, at block-row and -column `ibr` and `ibc`.  
   Use [`prepare`](@ref) to allocate memory for `global` and build the assembler `asm`.
""" 
function addin!(asm::SparseMatrixCSC{ùï´1,ùï´},out::SparseMatrixCSC{Tv,Ti},block::SparseMatrixCSC{Tv,Ti},ibr::ùï´,ibc::ùï´,factor::‚Ñù=1.) where{Tv,Ti<:Integer}
    # dichotomy to find ibv (index into asm.nzval)
    lo   = asm.colptr[ibc]         
    hi   = asm.colptr[ibc+1]-1
    local ibv # declare ibv so that ibv in the while scope survives the end of the loop 
    while true
        ibv  = div(lo+hi,2)
        aibr = asm.rowval[ibv]
        if ibr == aibr       break # correct ibv
        else
            hi ‚â§ lo && muscadeerror(@sprintf("BlockSparseAssembler pattern has no block [%i,%i]",ibr,ibc))
            if ibr > aibr    lo = ibv+1
            else             hi = ibv-1  
            end
        end
    end
    # addin the block
    aigv = asm.nzval[ibv]     
    for ilv ‚àà eachindex(aigv)  
        out.nzval[aigv[ilv]] += block.nzval[ilv] * factor
    end
end


"""
    addin!(asm,outvec,blockvec,ibr)

Add a full `block` vector into a large `outvec` full vector.  at block-row `ibr`.
Use [`prepare`](@ref) to create `asm`.

See also: [`prepare`](@ref)
""" 
function addin!(pgr::ùï´1,out::AbstractVector{Tv},block::Vector{Tv},ibr::ùï´,factor::‚Ñù=1.) where{Tv}
    for (ilv,igv)‚ààenumerate(pgr[ibr]:pgr[ibr+1]-1) 
        out[igv] += block[ilv] * factor
    end
end

# disassemble a block from a big-vector
disblock(pgc::ùï´1,v::Vector,ibc::ùï´) = view(v,pgc[ibc]:(pgc[ibc+1]-1))


"""
    sparser!(S::SparseMatrixCSC,keep::Function)

Eliminate terms that do not satisfy a criteria from the storage of a sparse matrix.
`S` will be mutated.
`keep` is a `Function` which to an index into `S.nzval` associate `true` if storage is to be kept for this term
and `false` otherwise.

Example

`sparser!(S,i->abs(S.nzval[i])>tol)`
`sparser!(S,i->pattern[i]         )`

In the first example, the keep function accesses `S.nzval[i]` before the term is mutated by `sparser!`.  If using `sparser!` to
reduce the storage of a collection of sparses with identical sparsity structure, care must be taken that the function must 
refer to the original structure of all sparses.  This can be done by creating a vector `pattern`, used in the second example.

"""
function sparser!(S::SparseMatrixCSC{Tv,Ti},keep::Function) where{Tv,Ti}
    ndrop               = 0
    @inbounds for icol  = 1:S.n
        colptr          = S.colptr[icol]
        S.colptr[icol] -= ndrop
        for inz         = colptr:S.colptr[icol+1]-1
            if keep(inz)
                S.nzval[ inz-ndrop] = S.nzval[ inz]
                S.rowval[inz-ndrop] = S.rowval[inz]
            else    
                ndrop  += 1
            end
        end
    end
    @inbounds S.colptr[S.n+1] -= ndrop
    nnz                 = length(S.nzval)
    resize!(S.nzval ,nnz-ndrop)
    resize!(S.rowval,nnz-ndrop)
end
